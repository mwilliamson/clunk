import stdlib/assertions.assertThat;
import stdlib/matchers.equalTo;
import mammoth/html;

testSuite "stripEmpty" {
    test "text nodes with text are not stripped" {
        var nodes = [html.text("H")];

        var result = html.stripEmpty(nodes);

        assertThat(result, equalTo([html.text("H")]));
    }

    test "empty text nodes are stripped" {
        var nodes = [html.text("")];

        var result = html.stripEmpty(nodes);

        assertThat(result, equalTo([]));
    }

    test "elements with non-empty children are not stripped" {
        var nodes = [html.element("p", #[], [html.text("H")])];

        var result = html.stripEmpty(nodes);

        assertThat(result, equalTo([html.element("p", #[], [html.text("H")])]));
    }

    test "elements with no children are stripped" {
        var nodes = [html.element("p", #[], [])];

        var result = html.stripEmpty(nodes);

        assertThat(result, equalTo([]));
    }

    test "elements with only empty children are stripped" {
        var nodes = [html.element("p", #[], [html.text("")])];

        var result = html.stripEmpty(nodes);

        assertThat(result, equalTo([]));
    }

    test "empty children are removed" {
        var nodes = [
            html.element("ul", #[], [
                html.element("li", #[], [html.text("")]),
                html.element("li", #[], [html.text("H")]),
            ]),
        ];

        var result = html.stripEmpty(nodes);

        assertThat(result, equalTo([
            html.element("ul", #[], [
                html.element("li", #[], [html.text("H")]),
            ]),
        ]));
    }

    test "self-closing elements are never empty" {
        var nodes = [html.element("br", #[], [])];

        var result = html.stripEmpty(nodes);

        assertThat(result, equalTo([html.element("br", #[], [])]));
    }

    test "force writes are never empty" {
        var nodes = [html.forceWrite()];

        var result = html.stripEmpty(nodes);

        assertThat(result, equalTo([html.forceWrite()]));
    }
}

testSuite "collapse" {
    test "collapsing does nothing to single text node" {
        var nodes = [html.text("Bluebells")];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([html.text("Bluebells")]));
    }

    test "consecutive fresh elements are not collapsed" {
        var nodes = [
            html.freshElement("p", #[], []),
            html.freshElement("p", #[], []),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.freshElement("p", #[], []),
            html.freshElement("p", #[], []),
        ]));
    }

    test "elements with different tag names are not collapsed" {
        var nodes = [
            html.collapsibleElement("p", #[], [html.text("One")]),
            html.collapsibleElement("div", #[], [html.text("Two")]),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.collapsibleElement("p", #[], [html.text("One")]),
            html.collapsibleElement("div", #[], [html.text("Two")]),
        ]));
    }

    test "elements with different attributes are not collapsed" {
        var nodes = [
            html.collapsibleElement("p", #[["id", "a"]], [html.text("One")]),
            html.collapsibleElement("p", #[], [html.text("Two")]),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.collapsibleElement("p", #[["id", "a"]], [html.text("One")]),
            html.collapsibleElement("p", #[], [html.text("Two")]),
        ]));
    }

    test "consecutive collapsible elements are collapsed if they have the same tag name and no attributes" {
        var nodes = [
            html.collapsibleElement("p", #[], [html.text("One")]),
            html.collapsibleElement("p", #[], [html.text("Two")]),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.collapsibleElement("p", #[], [html.text("One"), html.text("Two")]),
        ]));
    }

    test "consecutive collapsible elements are collapsed if they have the same tag name and same attributes" {
        var nodes = [
            html.collapsibleElement("p", #[["a", "1"], ["b", "2"]], [html.text("One")]),
            html.collapsibleElement("p", #[["b", "2"], ["a", "1"]], [html.text("Two")]),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.collapsibleElement("p", #[["a", "1"], ["b", "2"]], [html.text("One"), html.text("Two")]),
        ]));
    }

    test "children of elements are collapsed" {
        var nodes = [
            html.freshElement("div", #[], [
                html.collapsibleElement("p", #[], [html.text("One")]),
                html.collapsibleElement("p", #[], [html.text("Two")]),
            ]),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.freshElement("div", #[], [
                html.collapsibleElement("p", #[], [html.text("One"), html.text("Two")]),
            ]),
        ]));
    }

    test "children of collapsed element can collapse with children of previous element" {
        var nodes = [
            html.collapsibleElement("blockquote", #[], [
                html.collapsibleElement("p", #[], [html.text("One")]),
            ]),
            html.collapsibleElement("blockquote", #[], [
                html.collapsibleElement("p", #[], [html.text("Two")]),
            ]),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.collapsibleElement("blockquote", #[], [
                html.collapsibleElement("p", #[], [html.text("One"), html.text("Two")]),
            ]),
        ]));
    }

    test "collapsible element can collapse into previous fresh element" {
        var nodes = [
            html.freshElement("p", #[], []),
            html.collapsibleElement("p", #[], []),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.freshElement("p", #[], []),
        ]));
    }

    test "consecutive elements can collapse if second element has choice of tag names including main tag name of first element" {
        var nodes = [
            html.collapsibleElement("ol", #[], []),
            html.HtmlElement(html.HtmlTag(["ul", "ol"], #[], true, ""), []),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.collapsibleElement("ol", #[], []),
        ]));
    }

    test "consecutive elements cannot collapse if second element has tag name as non-main tag name of first element" {
        var nodes = [
            html.HtmlElement(html.HtmlTag(["ul", "ol"], #[], true, ""), []),
            html.collapsibleElement("ol", #[], []),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.HtmlElement(html.HtmlTag(["ul", "ol"], #[], true, ""), []),
            html.collapsibleElement("ol", #[], []),
        ]));
    }

    test "when separator is present then separator is prepended to collapsed element" {
        var nodes = [
            html.tagBuilder("pre").fresh().element([html.text("Hello")]),
            html.tagBuilder("pre").collapsible().withSeparator("\n").element([html.text(" the"), html.text("re")]),
        ];

        var result = html.collapse(nodes);

        assertThat(result, equalTo([
            html.freshElement("pre", #[], [
                html.text("Hello"),
                html.text("\n"),
                html.text(" the"),
                html.text("re"),
            ]),
        ]));
    }
}
